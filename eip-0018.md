# ErgoFund contracts

* Author: kushti
* Status: Proposed
* Created: 11-June-2021
* License: CC0
* Forking: not needed 

## Motivation 

Collecting funds in different contexts is very much needed for building common infrastructure and projects
 in the Ergo ecosystem. This EIP is proposing contracts and standardized boxes formats for announcing 
 crowdfunding campaigns and collecting funds.

## Overall Design

The design of the crowdfunding contracts and box templates below is centered around 
efficiency of blockchain scanning by an offchain application (implementing backend of ErgoFund service). 

## Campaign Registration

Crowdfunding campaign registration is controlled by a control box associated with an NFT which registers R4 and R5 
contain registration price and address (script) to pay for campaign registration.

Control box:

Contains campaign registration price in register R4 (long value) and script to pay for registration on register R5 
(byte array).

To register a new crowdfunding campaign, crowdfunding token must be bought (to compensate expenses for scanning, 
storing crowdfunding data, doing an UI) 

Sell contract:

```scala
{
 val controlBox = CONTEXT.dataInputs(0)
 
 // check control box NFT
 val properControlBox = controlBox.tokens(0)._1 == fromBase64("uuqgee/jRz3XRo5Vsg2pYceRVdqahII27Hpc8NdM+Zo=")

 val price = controlBox.R4[Long].get 
 val script = controlBox.R5[Coll[Byte]].get


 val inTokensCount = SELF.tokens(1)._2
 
 val selfOut = OUTPUTS(0)
 
 val validNFT = selfOut.tokens(0)._1 == fromBase64("SKsyra9+sTLHbAd/3rPfamaSQXFo8VQcg6VUXBDGP4E=") 
 val validOutTokenId = selfOut.tokens(1)._1 == fromBase64("B6V6SJ0YdzStTJYFFP28sXm+rlgid0lUrBVkCF5kHc4=") 
 val outTokensCount = selfOut.tokens(1)._2

 val validTokens = validNFT && validOutTokenId && (outTokensCount == inTokensCount - 1)

 val validScript = SELF.propositionBytes == selfOut.propositionBytes

 val rewardOut = OUTPUTS(1) 
 val validPayment = rewardOut.value >= price && rewardOut.propositionBytes == script

 properControlBox && validTokens && validScript && validPayment
}
```


Control box NFT id: baeaa079efe3473dd7468e55b20da961c79155da9a848236ec7a5cf0d74cf99a
Token sale NFT id: 48ab32adaf7eb132c76c077fdeb3df6a6692417168f1541c83a5545c10c63f81
Campaign identification token: 07a57a489d187734ad4c960514fdbcb179beae5822774954ac1564085e641dce

Tokensale box P2S address: r5wDC8T2yMcZWW8UzJoEsUD6fKjxe89SQPWCEHKLkjZLYAxLdrxYnBGvqXZDu8bZsurA3SGVhVQjcXYwVbtT2g8i91PmGbD2XNuoxT43bM56RSFGHDuyAzgE9GUtBegYbcYQ2XWy6ks6wHdq3cnBRWzf3WkXuR4LyuuDNN1jkQQE5NBBJfpVD6Eyuh1sSApMoJxf6Hv5jJjsUEMEkpZwyqjXzQBR1KUcp8nw7D88w135PfnDQVTAqmRAim1YEk35dvNtqBDAqaJK1ukRdcfbNe5axr8BRsn4wR9jX3fpuoh9q2FkmyvAWj2VWqYgacFvD9LwBVpRZi7nNBWVtp1

So to register campaign, one need to pass control box as a data input, sell contract among inputs, and create a box with 
campaign box data specified below in outputs.

## Campaign Box Data

For campaign, it is enough just to create box with ErgoFund token, campaign ID, campaign desc, script, recommended deadline, min to raise.

value >= 1 ERG (1000000000 nanoERG)
script = "4MQyMKvMbnCJG3aJ" (false proposition, so no one can spend)

*R4* - campaign ID (Int)
*R5* - campaign desc (byte array)
*R6* - campaign script (funds collected will go to this)
*R7* - fundraising deadline (Int, # of block, exclusive) 
*R8* - min value for successful fundraising (Long)  

https://explorer.ergoplatform.com/en/transactions/2e25bc0ea4d01108ab1cd76969f49022228b533a2ea50540f6cde6258029a510


example:

```json
[
  {
    "address": "4MQyMKvMbnCJG3aJ",
    "value": 100000000,
    "assets": [
      {
        "tokenId": "08fc8bd24f0eaa011db3342131cb06eb890066ac6d7e6f7fd61fcdd138bd1e2c",
        "amount": 1
      }
    ],
    "registers": {
      "R4": "0400",
      "R5": "0e00",
      "R6": "08cd0327e65711a59378c59359c3e1d0f7abe906479eccb76094e50fe79d743ccc15e6",
      "R7": "04a0be49",
      "R8": "0580d0acf30e"
    }
  }
]
```

## Pledge Contract

```scala
{
   val campaignId = SELF.R4[Int].get
   val backerPubKey = SELF.R5[SigmaProp].get
   val projectPubKey = SELF.R6[SigmaProp].get
   val deadline = SELF.R7[Int].get // height
   val minToRaise = SELF.R8[Long].get

   val fundraisingFailure = HEIGHT >= deadline && OUTPUTS(0).propositionBytes == backerPubKey.propBytes && OUTPUTS(0).value >= SELF.value 
   val enoughRaised = {(outBox: Box) => outBox.value >= minToRaise && outBox.propositionBytes == projectPubKey.propBytes && outBox.R4[Int].get == campaignId}

   val fundraisingSuccess = HEIGHT < deadline && enoughRaised(OUTPUTS(0))
   fundraisingFailure || fundraisingSuccess
}
```

address: XUFypmadXVvYmBWtiuwDioN1rtj6nSvqgzgWjx1yFmHAVndPaAEgnUvEvEDSkpgZPRmCYeqxewi8ZKZ4Pamp1M9DAdu8d4PgShGRDV9inwzN6TtDeefyQbFXRmKCSJSyzySrGAt16 

*R4* - campaign ID (Int)
*R5* - backer script
*R6* - campaign script (funds collected will go to this)
*R7* - fundraising deadline (Int, # of block, exclusive) 
*R8* - min value for successful fundraising (Long)


## TO-DO

* Contracts to collect funds in SigUSD and other tokens.  
* Contracts for campaigns with thousands of pledges.